set verbose off
set seed 12041996
include fdensity.gfn

EXAMPLE = 2   # Select an example to run

if EXAMPLE == 1
    # artificial, with dummy factor
    nulldata 8192
    d = uniform() < 0.4
    x = d ? normal() : 3 + normal()/2
    freq x --silent --plot=display
    boxplot x d --factorized --output=display
    bundles b = fdensity(x, d)
    print "three plots should have appeared"
    # print content of bundle
    print b[1]

elif EXAMPLE == 2
    # discrete numerical factor
    open keane.gdt --quiet
    fdensity(lwage, choice)
    print "a plot should have appeared"

    # Without factor series
    fdensity(lwage)
    print "a plot should have appeared"

elif EXAMPLE == 3
    # same as above, with string-valued factor
    open keane.gdt --quiet
    series str_ch = choice
    stringify(str_ch, strsplit("sch home wc bc serv"))

    fdensity(lwage, str_ch)
    print "a plot should have appeared"

elif EXAMPLE == 4
    # same as example 1, with different options
    nulldata 8192
    d = uniform() < 0.4
    x = d ? normal() : 3 + normal()/2
    my_title = "Smaller bandwidth, Epanechnikov kernel"
    bundle opts = _(scale = 0.8, control = 1, title = my_title,
                    monochrome = TRUE, auto_dashtype = TRUE,
                    linewidth = 2, grid = FALSE, cumulative = TRUE,
                    logscale = FALSE)

    fdensity(x, d, opts)
    print "a plot should have appeared"

elif EXAMPLE == 5
    # same as example 1, with different options
    nulldata 8192
    d = uniform() < 0.4
    x = d ? normal() : 3 + normal()/2
    my_title = "Automatic bandwidth and Epanechnikov kernel"
    opts = _(scale = 1, control = 1,
             title = my_title, add_opts = "set xrange[-2:4]")

    fdensity(x, d, opts)
    print "a plot with a restricted x-range should have appeared"

elif EXAMPLE == 6  # list of variables
    # artificial, with dummy factor
    nulldata 8192
    d = uniform() < 0.4
    list X = mat2list(mnormal($nobs, 4) .+ {d})
    bundles B = fdensity(X, d)
    print B

    # Now without factors
    fdensity(X)

elif EXAMPLE == 7 # list of series
    open mroz87 --quiet
    list L = WA HA WE HE
    fdensity(L, KL6)
endif
