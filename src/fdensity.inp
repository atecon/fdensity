function matrix fill (const matrix f,
                      const matrix xcoord)
    /*
    This function fills in missing values in a matrix using linear interpolation.
    Parameters:
      - f: The matrix containing the values to be filled.
      - xcoord: The matrix containing the corresponding x-coordinates for each value in f.
        Returns: The matrix with missing values filled using linear interpolation.
    */

    n = rows(f)
    valid = selifr(seq(1,n)', ok(f))
    nv = rows(valid)

    if nv < 2
        return {}
    endif

    to_fill = diff(valid)[2:] .> 1
    fills = selifr(valid[1:nv-1] ~ valid[2:nv], to_fill)
    ret = f
    nf = rows(fills)
    loop i = 1 .. nf
        lo = fills[i,1]
        hi = fills[i,2]
        xlo = xcoord[lo]
        xhi = xcoord[hi]
        d = (f[hi] - f[lo]) / (xhi - xlo)
        v = f[lo] + (xcoord[lo+1:hi-1] - xlo) * d
        ret[lo+1:hi-1] = v'
    endloop

    return ret
end function

function matrices fkd (const series x,
                       const series d,
                       const bundle opts)
    v = values(d)
    nv = rows(v)
    matrix f = {}
    matrix kept = {}

    if nv > opts.MAXVAL
        printf "Too many values for factor (%d, max. 12)\n", nv
        return defarray(f, kept)
    endif

    matrices ret = array(2)
    matrix X = {}
    matrix Y = {}

    loop i = 1 .. nv
        di = v[i]
        series tmp = d == di ? x : NA
        nn = nobs(tmp)

        if nn >= opts.MINOBS
            kept ~= i

            # TODO: We may want to store the optimal bandwidths
            if opts.scale == 1
                scalar scale = optimal_bandwidth(tmp, opts)
            else
                scalar scale = opts.scale
            endif

            matrix kd = kdensity(tmp, scale, opts.control)

            n = rows(kd)
            X |= kd[,1]
            Yi = mshape(NA, n, nv)
            Yi[,i] = kd[,2]
            Y |= Yi
        endif
    endloop
    nk = nelem(kept)

    if nk > 0
        # fill up the NAs
        scalar n = rows(X)
        X = msortby(seq(1, n)' ~ X, 2)
        ndx = X[,1]
        X = X[,2]
        Z = mshape(NA, n, nk)

        loop i = 1 .. nk
            j = kept[i]
            Z[,i] = fill(Y[ndx,j], X)
        endloop

        # set up the column names
        f = Z ~ X
        strs = strvals(d, 1)
        if nelem(strs)
            strings clabels = strs[kept]
        else
            matrix v = values(d)
            strings clabels = array(nk)
            loop i = 1 .. nk
                clabels[i] = sprintf("%s = %d", opts.dname, v[kept[i]])
            endloop
        endif
        clabels = clabels + defarray(opts.xname)
        cnameset(f, clabels)
        ret = defarray(f, kept)
    endif
    return ret
end function


function scalar optimal_bandwidth (const series y,
                                   const bundle opts)
    /* Compute the optimal bandwidth using the KdeBandwidth package.
    return: scalar, optimal bandwidth */

    scalar bw = kde_bandwidth({y},
                              opts.bw_method,
                              opts.bw_df,
                              opts.bw_skip_na,
                              opts.bw_verbose)

    return bw
end function


function bundle fkd_defopts (void)
    /* Bundle holding default values. */

    bundle ret = _(auto_dashtype = FALSE,
                   auto_pointtype = FALSE,
                   bw_method = "silverman",
                   bw_skip_na = TRUE,
                   bw_df = TRUE,
                   bw_verbose = FALSE,
                   control = 0,
                   dest = "display",
                   fontsize = 10,
                   fontsize_tics = 10,
                   grid = FALSE,
                   linewidth = 1,
                   MINOBS = 30,
                   MAXVAL = 12,
                   monochrome = FALSE,
                   nokey = FALSE,
                   pointsize = 0.75,
                   scale = 1,
                   single_yaxis = FALSE,
                   title = "",
                   ylabel = "",
                   xlabel = "",
                   add_opts = "")

    return ret
end function



function bundle fdensity (const series x,
                          const series d,
                          bundle opts_in[null])

    /*
    This function calculates the kernel density estimation for a given series.
    Parameters:
      - x: The series for which the kernel density is calculated.
      - d: The conditioning series.
      - opts_in: Optional bundle of options for the kernel density estimation.
    Returns:
      A bundle containing the kernel density estimation results and options.
    */

    bundle opts = fkd_defopts()
    if exists(opts_in)
        opts = opts_in + opts
    endif

    opts.xname = argname(x)
    if strlen(opts.xname) == 0
        opts.xname = "(anonymous)"
    endif

    opts.dname = argname(d)
    if strlen(opts.dname) == 0
        opts.dname = "(anonymous)"
    endif

    if strlen(opts.title)
        opts.title = opts.title
    else
        opts.title = sprintf("Kernel density of %s by %s",
                             opts.xname, opts.dname)
    endif

    matrices f_k = fkd(x, d, opts)
    kept = f_k[2]
    bundle ret = opts
    ret.err = 0

    if nelem(kept) == 0
        printf "Invalid conditioning\n"
        ret.err = 1
        return ret
    endif

    matrix f = f_k[1]
    ret.kept = kept
    ret.f = f

    plot_fdensity(ret, opts)

    return ret
end function


function void plot_fdensity (const bundle input,
                             const bundle opts)

    /*
    * Function: plot_fdensity
    * -----------------------
    * Plots the density function using the given input data and options.
    *
    * Parameters:
    *    input - A bundle containing the input data.
    *    opts - A bundle containing the plot options.
    *
    * Returns:
    *    None
    */

    matrix fdata = input.f
    string dest = opts.dest
    string plot_options = opts.auto_pointtype == TRUE ? "with-lp " : \
                         "with-lines "
    plot_options += opts.single_yaxis == TRUE ? "single-yaxis " : ""

    string linetype_options = ""
    if opts.auto_dashtype || opts.auto_pointtype || opts.monochrome
        linetype_options = "set for [i=1:8] linetype i "
        if opts.auto_dashtype
            linetype_options += "dashtype i "
        endif
        if opts.auto_pointtype
            linetype_options += sprintf("pointtype i ps %g ", opts.pointsize)
        endif
        if opts.monochrome
            linetype_options += "lc rgb 'black"
        endif
    endif

    string key_opts = opts.nokey == TRUE ? "set nokey" : ""
    string grid_opts = opts.grid == TRUE ? "set grid" : ""

    plot fdata
        option @plot_options
        printf "%s", linetype_options
        printf "set termoption lw %d", opts.linewidth
        printf "set title \"%s\" font ',%d'", opts.title, opts.fontsize
        printf "set ylabel \"%s\" font ',%d'", opts.ylabel, opts.fontsize
        printf "set xlabel \"%s\" font ',%d'", opts.xlabel, opts.fontsize
        printf "set xtics font ',%d'", opts.fontsize_tics
        printf "set ytics font ',%d'", opts.fontsize_tics
        printf "%s", key_opts
        printf "%s", grid_opts
        printf "%s", opts.add_opts
    end plot --output="@dest"
end function



function bundle fdensity_GUI (const series x,
                              const series d,
                              scalar scale[0::1],
                              int control[0:1:0] {"Gaussian", "Epanechnikov"})
    xname = argname(x)
    dname = argname(d)
    title = sprintf("Kernel density of %s by %s", xname, dname)

    bundle opts = fkd_defopts()
    if exists(opts_in)
        opts = opts_in + opts
    endif

    matrices f_k = fkd(x, d, xname, dname, scale, control)
    kept = f_k[2]
    bundle ret = opts
    ret.err = 0

    if nelem(kept) == 0
        funcerr "Invalid conditioning"
        ret.err = 1
        return ret
    endif

    matrix f = f_k[1]
    ret.kept = kept
    ret.f = f

    plot f
        option with-lines
        printf "set title \"%s\"", title
    end plot --output=display

    return ret
end function


function matrix remove_nas (const matrix y)
    /* Remove missing values (NAs) from a matrix.

    Parameters:
    y (matrix): Input matrix with potential missing values.

    Returns:
    matrix: The input matrix with all NAs removed. */

    return selifr(y, ok(y))
end function


function scalar kde_bandwidth (matrix input,
                               string method,
                               const bool df_correction[0::1],
                               const int skip_na[0:1:1],
                               const int verbose[0:1:0])
    /*
       Calculate the bandwidth for kernel density estimation (KDE) using
       various methods for uni-variate data only (at the moment).

       Parameters:
       input (matrix): Input data as a column vector.
       method (string): Method for bandwidth selection. Supported values: "scott" or "silverman".
       df_correction (bool): Indicator for degrees of freedom correction
       when computing the standard devation. FALSE not to correct, TRUE to
       correct.
       skip_na (bool): Indicator for skipping missing values. 0 to include missing values, 1 to remove them.
       verbose (bool): Indicator for printing bandwidth selection results. FALSE to disable, TRUE to enable.

       Returns:
       scalar: The calculated bandwidth for KDE.

       Raises:
       Error: If the input data has more than one column.
       Error: If an unsupported method of bandwidth selection is specified.
    */

    string method = tolower(method)
    errorif(cols(input) > 1, "Only column vector is supported")
    matrix input = skip_na == TRUE ? remove_nas(input) : input
    scalar n = rows(input)
    scalar std_dev = sdc(input, df_correction)

    if method == "scott"
        scalar bandwidth = bandwidth_scott(std_dev, n)
    elif method == "silverman"
        # This value makes sure that gretl's built-in kdensity() automatically
        # applies Silverman's method. No need to do the job on our own.
        scalar bandwidth = 1

        # Uncomment the following for a manual computation of Silverman's method
        #scalar bandwidth = bandwidth_silverman(input, std_dev, n)
    else
        errorif(TRUE, "Unsupported method of bandwidth selection.")
    endif

    if verbose
        bandwidth_print(method, df_correction, skip_na, bandwidth)
    endif

    return bandwidth
end function


function void bandwidth_print (const string method,
                               const int df_correction[0::1],
                               const int skip_na[0:1:1],
                               const scalar bandwidth)
    /* Print out results. */

    string df_corr = df_correction == TRUE ? "TRUE" : "FALSE"
    string str_skip_na = skip_na == TRUE ? "TRUE" : "FALSE"

    printf("\n*** Bandwidth Selection Results ***\n")
    printf("Method: %s\n", method)
    printf("Degrees of freedom correction: %s\n", df_corr)
    printf("Skip NAs: %s\n", str_skip_na)
    printf("Bandwidth: %g\n\n", bandwidth)
end function


function scalar bandwidth_scott (const scalar std_dev,
                                 const scalar n)
    /* Calculate the bandwidth for kernel density estimation (KDE) using Scott's rule.

    Parameters:
    std_dev (scalar): Standard deviation of the input data.
    n (scalar): Number of observations in the input data.

    Returns:
    scalar: The calculated bandwidth for KDE using Scott's rule. */

    scalar bandwidth = 3.49 * std_dev * n ^ (-0.333)
    return bandwidth
end function


# This function is never called as we rely on gretl's built-in computation
# But keep it for pedagocical reasons
function scalar bandwidth_silverman (const matrix y,
                                     const scalar std_dev,
                                     const scalar n)
    /* Calculate the bandwidth for kernel density estimation (KDE) using Silverman's rule.

    Parameters:
    y (matrix): Input data as a column vector.
    std_dev (scalar): Standard deviation of the input data.
    n (scalar): Number of observations in the input data.

    Returns:
    scalar: The calculated bandwidth for KDE using Silverman's rule. */

    scalar iqr = quantile(y, 0.75) - quantile(y, 0.25)
    matrix value = min(std_dev ~ (iqr / 1.34))
    scalar bandwidth = 0.9 * value * (n^(-0.2))

    return bandwidth
end function
